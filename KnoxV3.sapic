/*******************************************************************************
	Remote attestation Framework
	AUTHORS
	
	
*******************************************************************************/

theory Framework_KnoxV3
begin

builtins: signing, asymmetric-encryption

functions: check_rep/2,  get_rep/1, rep/2 [private], create_certificate/3, verify_certificate/2 , get_tbsInfo_certificate/1

equations:
    check_rep(rep(x.1, x.2), x.2) = true,
    get_rep(rep(x.1, x.2)) = x.1,

    create_certificate(SubjectTBSCertificateInfo,SubjectPup,IssuerSk) 
      = < SubjectTBSCertificateInfo, SubjectPup, sign(<SubjectTBSCertificateInfo, SubjectPup>,IssuerSk) >,

    verify_certificate( 
    < SubjectTBSCertificateInfo, pk(SubjectSk), sign(<SubjectTBSCertificateInfo, pk(SubjectSk)>,IssuerSk) >
     , < IssuerTBSCertificateInfo, pk(IssuerSk), sign(<IssuerTBSCertificateInfo, pk(IssuerSk)>,IssuerRootSk) >
          ) = true,

    get_tbsInfo_certificate( 
        < SubjectTBSCertificateInfo, pk(SubjectSk), sign(<SubjectTBSCertificateInfo, pk(SubjectSk)>,IssuerSk) >
          ) = SubjectTBSCertificateInfo
      


/***************************************** Generic *****************************************/

let AppCode = 
	[Nonce_Sent(devId,nonce)]--[]-> [];

	event NonceReceived(nonce);

	//attestation_agent send app data to secure world to start an attestation
	new ~sessionID;
	[]--[]-> [TOSW(~sessionID,~deviceId,swId,nonce,'App1PackageName','App1Content',sign(advk, <'App1PackageName','App1Content',pk(advk)>))];
	[ FROMSW(~sessionID,hid) ]--[]-> [];
	out(hid);
	0

let HonestAppInstallation =
	event Checked('HonestAppInstallation');
	//install an application
	[!App_Published(devId,<'App1PackageName','App1Content',pk(advk),
	                   sign(advk, <'App1PackageName','App1Content',pk(advk)>)>)]--[]-> [];

	event Application_Installed(~deviceId,'App1PackageName','App1Content',sign(advk, <'App1PackageName','App1Content',pk(advk)>));

	!AppCode // TOM: App code separate from installation 


let ArbitraryAppCode =
	in(nonce);
	event NonceReceived(nonce);
	//attestation_agent send app data to secure world to start an attestation
	new ~sessionID;
	[]--[]-> [TOSW(~sessionID,~deviceId,swId,nonce,packagename,~content,appSignature)];
	[ FROMSW(~sessionID,hid) ]--[]-> [];
	out(hid);
	0

let ArbitraryAppInstallation =
	event Checked('ArbitraryAppInstallation');
	//install an application
	in(<packagename, devKey>); //TOM Attacker can pick the app name and the dev key.
	//REMOVED: new ~hash;
	//REMOVED: let appSignature = ~hash in

	new ~content;  //TOM: contents are different from any honest app, so we model as a free name

	let appSignature = sign(devKey, <packagename,~content,pk(devKey)>) in
	let app = <packagename,~content,pk(devKey),appSignature> in
	out(app);
	event Arbitrary_App_Created(packagename,appSignature);
	event Application_Installed(~deviceId,packagename,~content,appSignature);

	!ArbitraryAppCode


let Device =

	//Create device reference
	new ~deviceId;   // TOM: Is the device ID secure?

	//send the device reference , allows an adversary to tamper the device
	out(~deviceId);
	
	//setup a secureWorld
	[]--[]-> [SetUPSW(~deviceId)];
	[FinishSetUPSW(~deviceId,swId)]--[]->[];
	event A_finished();

	(!HonestAppInstallation | !ArbitraryAppInstallation) //TOM


/***************************************** Knox V3 *****************************************/

let AtServerKV3 =
	!(

		//Retrive blob
		[HttpsToAt(tsid,rep(<atId,hardwareMeasurement,nonce,packagename,appSignature, deviceId>,<~skAT,'Trustzone'>))]--[]-> [];

		//refernce blob to an hid
		new ~hid;
		//Send hid for the TZ
		[]--[]-> [HttpsFromAT(tsid,~hid)];

		//Retrive hid from the developer server and verfiy it
        [TOAT(sid,~hid)]--[]-> [];

		//create a verdict  and send it to the developer server
		let verdict = <atId,hardwareMeasurement,nonce,packagename,appSignature, deviceId> in
        []--[]-> [FRAT(sid,verdict)];

		event T_finish() 
	)


let KnoxV3TA =

	new ~swId; // We have different secure world and device ID, because it's possible to make different devices have the same ID, therefore 

	//setup a SecureWorld
	[SetUPSW(deviceId)]--[]->[FinishSetUPSW(deviceId,~swId)];

	//attempt to unlock bootloader
	in(attempt);

	!(
		//attestation_agent send app data to secure world, start an attestation
		[TOSW(sessionID, deviceId,~swId,nonce,packagename,content,appSignature)]--[]->[];

		new ~tsid; //  attestation server hid session id
		new ~atId; // attestation id
		event Attestating_App(~atId,packagename,appSignature,deviceId);

		if(attempt = 'unlockBootloader') then
			let hardwareMeasurement = 'invalid' in
			// Software measurment not used in Knox
			//in(customSoftwareMeasurement);
			//let softwareMeasurement =  customSoftwareMeasurement in
			let blob = rep(<~atId,hardwareMeasurement,nonce,packagename,appSignature, deviceId>,<~skAT,'Trustzone'>) in 
			///send attestation report to the attestation server
			[]--[]-> [HttpsToAt(~tsid,blob)];
			[HttpsFromAT(~tsid,hid)]--[]-> [];

			///return attestation report to the app
			event DeviceStatus(~atId,deviceId,hardwareMeasurement);
			[]--[]->[ FROMSW(sessionID,hid) ];
			event S_finish();
			0
		else
			let hardwareMeasurement = 'valid' in
			let blob = rep(<~atId,hardwareMeasurement,nonce,packagename,appSignature, deviceId>,<~skAT,'Trustzone'>) in 
			///send attestation report to the attestation server
			[]--[]-> [HttpsToAt(~tsid,blob)];
			[HttpsFromAT(~tsid,hid)]--[]-> [];

			///return attestation report to the app
			event DeviceStatus(~atId,deviceId,hardwareMeasurement);
			[]--[]->[ FROMSW(sessionID,hid) ];
			event S_finish();
			0
			
	)

let DevServerProtocolCode =
	// create a nonce
	new ~nonce;
	event NonceGenerated(~nonce);
	[]--[]-> [Nonce_Sent(~devId,~nonce)];
	out(~nonce);

	//retrive hid from clients
	in(hid);

	//create developer-attestation server session id, to keep track or pair of request-response message  
	new ~sid;
	//send blob to the attestation server
	[]--[]-> [TOAT(~sid,hid)];
	
	//retrieve the verfied report the attestation server, pattern matching is used for checking the status
	[FRAT(~sid,<atId,hardwareMeasurement,~nonce,fpackagename,fappSignature, deviceId>)]--[]-> [];
	

	//attestation finished		
	if (appSignature = fappSignature) & (packagename = fpackagename)  then
		event Verdict_app(atId,deviceId,hardwareMeasurement,'valid');
		event D_finish() 
	else
		event Verdict_app(atId,deviceId,hardwareMeasurement,'invalid');
		event D_finish() 
	


let DevServerKV3 =

	//creating and publishing the application
	new ~advk; // Dev server signing
	new ~devId; 
	// new ~content; REMOVED
	//new ~packagename;
	let packagename = 'App1PackageName' in
	let appSignature = sign(~advk, <packagename,'App1Content',pk(~advk)>) in 
	let app = <packagename,'App1Content',pk(~advk),appSignature> in
	out(app);
	event App_Created(packagename,appSignature);

	[]--[]-> [!App_Published(~devId,app)];
	
	!DevServerProtocolCode

/*************************************** Generic code *************************************************/
	
// Main process starts here

/***************************************** choose a platform ***************************************************
* platform				|		SafetyNet			|		KnoxV2		|	KnoxV3			|	Key Attestation
* --------------------------------------------------------------------------------------------------------------
* AttestationAgent		: GooglePlay | SafetyNetTA	|	KnoxV2TA		|	KnoxV3TA		|	KeyMasterTA
* AtServer				:		AtServerSN			|	AtServerKV2		|	AtServerKV3		|	AtServerKA
* DevServer				:		DevServerSN			|	DevServerKV2	|	DevServerKV3	|	DevServerKA
***************************************************************************************************************/


let AttestationAgent =
	KnoxV3TA

let AtServer = 
	AtServerKV3

let DevServer = 
	DevServerKV3



//create pair attestation keys to be used by secureWorld for signing, and attestation server for verification
new ~skAT;

(!Device || !DevServer || AtServer || !AttestationAgent)

// for all valid attestation => (device must be not rooted and app must be not repacked).
lemma DeviceAndAppIntegrity:
	"(All atId deviceId #i. Verdict_app(atId,deviceId,'valid','valid') @ i ==> 
	( 
	(Ex #j. DeviceStatus(atId,deviceId,'valid') @ j & j < i) 
	& (Ex packagename appSignature #j #k. 
			 App_Created(packagename,appSignature) @ j 
			 & Attestating_App(atId,packagename,appSignature,deviceId)@ k)
	))"


// for all failed application integrity check only  => (device must be valid and there an app created, an app attested and either the hash is different or packagename).
lemma RepackedAppIntegrity:
	"(All atId deviceId #i. Verdict_app(atId,deviceId,'valid','invalid') @ i ==> 
	( 
	(Ex #j. DeviceStatus(atId,deviceId,'valid') @ j & j < i) 
	& (Ex packagename fpackagename appSignature fappSignature #j #k. 
			 App_Created(packagename,appSignature) @ j 
			 & Attestating_App(atId,fpackagename,fappSignature,deviceId)@ k
			 & (
					(not(appSignature = fappSignature))
					| (not(packagename = fpackagename))
			 	)
			 
		)
	))"

// Check if nonce are unique 
lemma UniqueNonce:
	"All n #i #j. NonceGenerated(n) @i & NonceGenerated(n) @j ==> #i=#j"
	
//Sanity check: Attestation finished with valid device integirty and application integirty. 
lemma Correctness_Both_Valid:
exists-trace
	"Ex atId deviceId #i. Verdict_app(atId,deviceId,'valid','valid') @ i"

//Sanity check: Attestation finished with at least failed device integirty
lemma Correctness_Rooting_Device:
exists-trace
	"Ex atId any deviceId #i. Verdict_app(atId,deviceId,'invalid',any) @ i"

//Sanity check: Creation of an honest application 
lemma Correctness_Honest_App:
exists-trace
	"Ex #i. Checked('HonestAppInstallation') @ i"

//Sanity check: Creation of an arbitrary application 
lemma Correctness_Arbitrary_App:
exists-trace
	"Ex #i. Checked('ArbitraryAppInstallation') @ i"
	
end


==============================================================================
summary of summaries:

analyzed: framework_knoxV3.sapic

  DeviceAndAppIntegrity (all-traces): verified (9 steps)
  RepackedAppIntegrity (all-traces): verified (8 steps)
  UniqueNonce (all-traces): verified (8 steps)
  Correctness_Both_Valid (exists-trace): verified (5 steps)
  Correctness_Rooting_Device (exists-trace): verified (6 steps)
  Correctness_Honest_App (exists-trace): verified (4 steps)
  Correctness_Arbitrary_App (exists-trace): verified (4 steps)

==============================================================================