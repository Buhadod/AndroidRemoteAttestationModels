/*******************************************************************************
	Knox v2
	AUTHORS
	
	Sample message using SAPIC
	

	Protocol:
	1. C -> S: aenc(k, pkS)
	2. C <- S: h(k)

	Notation:
	
	pmk = Pairwise Master Key
	ptk = Pairwise Transient Key, composed of:
		kck = Key Confirmation Key
		kek = Key Encryption Key
		tk = Temporal Key
	gtk = Group Temporal Key
	
	A_  = Events only in authenticator process
	S_  = Events only in supplicant process
	
*******************************************************************************/

theory CPT
begin

builtins: signing, asymmetric-encryption

functions: create_certificate/3, verify_certificate/2 , get_tbsInfo_certificate/1, get_public_key_certificate/1

equations:
    create_certificate(SubjectTBSCertificateInfo,SubjectPup,IssuerSk) 
      = < SubjectTBSCertificateInfo, SubjectPup, sign(<SubjectTBSCertificateInfo, SubjectPup>,IssuerSk) >,

    verify_certificate( 
    < SubjectTBSCertificateInfo, pk(SubjectSk), sign(<SubjectTBSCertificateInfo, pk(SubjectSk)>,IssuerSk) >
     , < IssuerTBSCertificateInfo, pk(IssuerSk), sign(<IssuerTBSCertificateInfo, pk(IssuerSk)>,IssuerRootSk) >
          ) = true,

    get_tbsInfo_certificate( 
        < SubjectTBSCertificateInfo, pk(SubjectSk), sign(<SubjectTBSCertificateInfo, pk(SubjectSk)>,IssuerSk) >
          ) = SubjectTBSCertificateInfo,

	get_public_key_certificate( 
      < SubjectTBSCertificateInfo, pk(SubjectSk), sign(<SubjectTBSCertificateInfo, pk(SubjectSk)>,IssuerSk) >
          ) = pk(SubjectSk)
      

let SecureWorld =

	//Create device reference
	new ~deviceId;

	//receive an adversary's attempt to compomised the device
	in(<status,customSoftwareMeasurement>);

	//Request attestation server
	[!RequestCreatingIntermediateCertificate(rootSk,rootCertificate)]--[]->[];
	
	//Create keymaster certficate
	new ~keymasterSk;
	let  intermediateCertificate = create_certificate('TbsKeyStoreInfo',pk(~keymasterSk),rootSk) in

	!(
		//recieve nonce from the App
		[AndroidIntentTOSW(asid,nonce,packagename,appSignature)]--[]->[];
		
		new ~key;
		[]--[]->[IntentTOKS(asid,~key)];

		let subjectCertificate = create_certificate(<hardwareMeasurement,nonce,packagename,appSignature>,pk(~key),~keymasterSk) in
		let certificateChain = <subjectCertificate, intermediateCertificate, rootCertificate> in 

		if(status = 'tamper') then
			let hardwareMeasurement = 'invalid' in
			let softwareMeasurement = customSoftwareMeasurement in
			let subjectCertificate = create_certificate(<hardwareMeasurement,nonce,packagename,appSignature>,pk(~key),~keymasterSk) in
			let certificateChain = <subjectCertificate, intermediateCertificate, rootCertificate> in 
			//send attestation report to the app
			[]--[]->[AndroidIntentFRSW(asid,~deviceId,hardwareMeasurement,certificateChain)];
			event S_finish();
			0
		else
			let hardwareMeasurement = 'valid' in
			let softwareMeasurement = 'valid' in
			let subjectCertificate = create_certificate(<hardwareMeasurement,nonce,packagename,appSignature>,pk(~key),~keymasterSk) in
			let certificateChain = <subjectCertificate, intermediateCertificate, rootCertificate> in 
			//send attestation report to the app
			[]--[]->[AndroidIntentFRSW(asid,~deviceId,hardwareMeasurement,certificateChain)];
			event S_finish()
		
	)

let KeyStore =
	!(
		[IntentTOKS(asid,key)]--[]->[];
		event K1_finish();

		[AndroidIntentTOKS(asid,cipher)]--[]->[];
		event K2_finish();

		[]--[]->[AndroidIntentFRKS(asid,adec(key,cipher))];
		event K3_finish()
	)

let App =

	new ~appId;
	//install an application
	in(<packagename,content,sign(key,<packagename,content>)>);

	//retrive the developer channel public key for secure coumincation
	[GetDeveloperkey(dvpk,appSignature)]--[]->[];
	
	!(
        
		//Create a session key to cominictae with the developer securely
		new ~ask;

		//retreive a nonoce from the developer server
		in(<'nonce_response',nonce>);

		//create a android intent session id, to keep track of the intent session during cominication with attestation agent in the TEE 
		new ~asid;

		//Send nonce to attestation agent and request measuring the device status 
		[]--[]->[AndroidIntentTOSW(~asid,nonce,packagename,sign(key,<packagename,content>))];

		//retrive a certificateChain represent the device measurment
		[AndroidIntentFRSW(~asid,deviceId,hardwareMeasurement,certificateChain)]--[]->[];

		//send the certificateChain to the developer server 
		out(<'certificateChain_request',certificateChain>);

		//retrive the secret from the developer server as a response from sending certificateChain
		in(<'certificateChain_response',cipher>);

		//send cipher to keystore
		[]--[]->[AndroidIntentTOKS(~asid,cipher)];

		//receive secret from keystore
		[AndroidIntentFRKS(~asid,secret)]--[]->[];

		event CipherReceived(cipher);
		event SecretReceived(secret);
		event SecretReceivedToApp(secret,~appId);
		event SecretReceivedToDevice(secret,deviceId);

		event A_finish();
		
		//leak secret if app repacked
		if not(appSignature = sign(key,<packagename,content>)) then
			event ApplicationTampered(~appId,secret);
			event DeviceTampered(deviceId,secret);
			event AXA_finish();			
			out(secret)
		else
			//leak if device tampered 
			if(hardwareMeasurement = 'invalid') then
				event DeviceTampered(deviceId,secret);
				event AXR_finish();
				out(secret)
			else
				event AA_finish()
		
	)


//todo : check if blob leaked, what goes wrong

let DevServer =

	//creating and publising the application
	new ~advk;
	new ~content;
	new ~packagename;
	let appSignature = sign(~advk, <~packagename,~content>) in
	let app = <~packagename,~content,appSignature> in
	out(app);

	//create a developer's pair key for secure cominucaion with clients
	new ~dvk;
	let dbpk = pk(~dvk) in

	//send it public key publically, allows advsery to cominuncate with the developer server
	out(dbpk);

	//send the publick key privatley for  client who request it by URL (authetnication)
	[]--[]->[GetDeveloperkey(pk(~dvk),appSignature)];
	
	//create a secret to be sent inexchange for a valid report
	new ~secret;
	event SecretGenerated(~secret);


	!(
		// create a nonce
		new ~nonce;
		event NonceGenerated(~nonce);

		//send nonce to to clients encrypted with their session key
		out(<'nonce_response',~nonce>);


		//retrive blob from clients
		in(<'certificateChain_request',<subjectCertificate, intermediateCertificate, rootCertificate>>);

		[!GetRootCertificate(rootCertificate)]--[]->[];
		
		let verification1 = verify_certificate(subjectCertificate, intermediateCertificate) in
      	let verification2 = verify_certificate(intermediateCertificate, rootCertificate) in
      	let verification3 = verify_certificate(rootCertificate, rootCertificate) in
		let tbsInfo = get_tbsInfo_certificate(subjectCertificate) in 
		let ppk = get_public_key_certificate(subjectCertificate) in
		
		new ~vsid;
		[]--[]->[VerficationCertificatehain('verification',<~vsid,verification1,verification2,verification3,<tbsInfo>>)];
		[VerficationCertificatehain('verification',<~vsid,true,true,true,<'valid',~nonce,~packagename,appSignature>>)]--[]->[];
		out(<'certificateChain_response',aenc(ppk,~secret)>);

		event CipherGenerated(aenc(ppk,~secret));
		event D_finish() 
	)
	
let AtServer =
	new ~rootSk;
	let rootCertificate = create_certificate('TbsInfo',pk(~rootSk),~rootSk) in

	//share root certficate	
	[]--[]->
	[
		!RequestCreatingIntermediateCertificate(~rootSk,rootCertificate),
		!GetRootCertificate(rootCertificate)
	];

	!(
		event T_finish() 
	)

// Main process starts here

//create pair attetsation keys to be used by secreworld for sigining, and attestation server for verfication
new ~skAT;
let pkAT = pk(~skAT) in


(!SecureWorld ||!App || !DevServer || AtServer || KeyStore)




lemma SecretSecrecy: 
        "(not ( Ex secret #i #k. SecretGenerated(secret) @i & KU(secret) @k ))"

lemma ApplicationIntegrity:
	"(not ( Ex secret appId #i #k. SecretGenerated(secret) @i & ApplicationTampered(appId,secret) @k ))"

lemma DeviceIntegrity:
	"(not ( Ex secret deviceId #i #k. SecretGenerated(secret) @i & DeviceTampered(deviceId,secret) @k ))"

lemma UniqueNonce:
	"All n #i #j. NonceGenerated(n) @i & NonceGenerated(n) @j ==> #i=#j"


	
//Sanity check: Developer server finsihed
lemma Correctness_D:
exists-trace
	"Ex #i. D_finish()@i"

//Sanity check: Secure world/device Finished
lemma Correctness_S:
exists-trace
	"Ex #i. S_finish()@i"

//Sanity check: Application finished
lemma Correctness_A:
exists-trace
	"Ex #i. A_finish()@i"

//Sanity check: Application Finshed with no tampere
lemma Correctness_AA:
exists-trace
	"Ex #i. AA_finish()@i"

//Sanity check: Application Finshed with app tampered 
lemma Correctness_AXA:
exists-trace
	"Ex #i. AXA_finish()@i"

//Sanity check: Application Finshed with device tampered
lemma Correctness_AXR:
exists-trace
	"Ex #i. AXR_finish()@i"
	
//Sanity check: Attesation server finished
lemma Correctness_T:
exists-trace
	"Ex #i. T_finish()@i"
	
lemma Correctness_K1:
exists-trace
	"Ex #i. K1_finish()@i"

lemma Correctness_K2:
exists-trace
	"Ex #i. K2_finish()@i"

lemma Correctness_K3:
exists-trace
	"Ex #i. K3_finish()@i"

lemma Correctness_Secret:
exists-trace
	"Ex secret #i. SecretReceived(secret)@i"
end